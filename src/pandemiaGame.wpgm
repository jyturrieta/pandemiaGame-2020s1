import wollok.game.*
import simulacion.*
import agenteDeSalud.*


object pandemiaGameConfig {
	method height() = 4
	method width() = 6
	method title() = "Simuléichon"
	
	method configureGame() 
	{
		self.configureGameParameters()
		self.agregarManzanas()
		// agregar las teclas de acciones
		// después, al agente de salud y sus teclas de acciones y movimiento
		self.agregarAgenteDeSalud()
		keyboard.o().onPressDo{simulacion.estadoDeSimulacion()}
		keyboard.i().onPressDo{simulacion.agregarInfectado()}
		keyboard.p().onPressDo{simulacion.pasarUnDia()}
		
		
		keyboard.d().onPressDo{ if(agenteDeSalud.position().x() < self.width()-1) {agenteDeSalud.moverseDerecha()} }
        keyboard.a().onPressDo{ if(agenteDeSalud.position().x() > 0) {agenteDeSalud.moverseIzquierda()} } 
        keyboard.w().onPressDo{ if(agenteDeSalud.position().y() < self.height()-1) {agenteDeSalud.moverseArriba() } }
        keyboard.s().onPressDo{ if(agenteDeSalud.position().y() > 0) {agenteDeSalud.moverseAbajo()} }
        
        keyboard.j().onPressDo{agenteDeSalud.aislarSintomaticos() game.say(agenteDeSalud, "Aislado maquinola")}
        keyboard.k().onPressDo{agenteDeSalud.mandarARespetarCuarentena() game.say(agenteDeSalud, "#QuedateEnCasa")}
        
	}
		
	method configureGameParameters() {
		game.height(self.height())
		game.width(self.width())
		game.title(self.title())
	}
	
	method agregarManzanas() {
		(1..game.width()).forEach({x => 
			(1..game.height()).forEach({y => 
				const nuevaManzana = simulacion.crearManzana()
				nuevaManzana.position(game.at(x-1,y-1))
				simulacion.agregarManzana(nuevaManzana)
				game.addVisual(nuevaManzana)
			})
		})
	}
	method agregarAgenteDeSalud()
	{
		agenteDeSalud.position(game.origin())
		game.addVisual(agenteDeSalud)
	}
}

program pandemiaGame {
	pandemiaGameConfig.configureGame()
	game.start()
}

